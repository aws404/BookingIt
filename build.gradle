plugins {
	id "fabric-loom" version "1.+"
	id "com.modrinth.minotaur" version "2.+"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

version = System.getenv("VERSION") ? System.getenv("VERSION") : "unversioned"

repositories {
	maven { url = "https://jitpack.io" }
}

sourceSets {
	testmod {
		compileClasspath += main.compileClasspath
		runtimeClasspath += main.runtimeClasspath
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modImplementation include("com.github.Chocohead:Fabric-ASM:v${project.mm_version}") {
		exclude (group: "net.fabricmc.fabric-api")
	}

	testmodImplementation sourceSets.main.output
}

loom {
	runs {
		testmodClient {
			client()
			ideConfigGenerated project.rootProject == project
			name = "Testmod Client"
			source sourceSets.testmod
		}
		testmodServer {
			server()
			ideConfigGenerated project.rootProject == project
			name = "Testmod Server"
			source sourceSets.testmod
		}
	}

	accessWidenerPath = file("src/main/resources/booking_it.accesswidener")
	accessWidenerPath = file("src/testmod/resources/bi_test.accesswidener")
}

modrinth {
	token = System.getenv("MODRINTH_TOKEN")
	failSilently = true
	projectId = project.project_id
	versionNumber = version
	versionName = System.getenv("VERSION_NAME") ? System.getenv("VERSION_NAME") : version
	versionType = System.getenv("ALPHA") ? "alpha" : "release"
	changelog = System.getenv("CAHNGELOG")
	uploadFile = "./build/libs/BookingIt-${version}.jar"
	dependencies {
		required.project "P7dR8mSH" // Mark Fabric API as a required dependency
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}